      SUBROUTINE PSVRAYIN (IA,ICS,ICM,ICI,i670,i400,i220,
     #	ILAY,NP,NS,ID,ISS,IRR,NN)
C IA    - CHARACTER STRING WITH NAME OF RAY E.G. 'PKiKP'
C ICS   - SOURCE LAYER INDEX
C ICM   - CORE-MANTLE INTRFACE INDEX
C ICI   - CORE-INNER CORE INTERFACE INDEX
c i670  - 670 interface index
c i400  - 400 interface index
c i220  - 220 interface index
C ILAY  - MAXIMUM NUMBER OF LAYERS IN MODEL
 
C OUTPUT:
C NP(I) - NUMBER OF P-WAVE LEGS IN LAYER I, INCLUDING SOURCE LEG.
C NS(I) -           S-WAVE
C ID(J,I) - CONVERSION COEFFICIENTS AT I'TH INTERFACE
C ISS   - SOURCE INDEX 1=P DOWN,2=S DOWN,5=P UP,6=S UP.
C IRR   - RECEIVER INDEX 9=P UP,10=S UP.
C NN    - NUMBER OF LAYERS IN WHICH RAY IS DEFINED.
 
C SUBROUTINE TO DECODE RAY NAME AND PRODUCE RAY DEFINITION IN INTEGER
C FORM .
C ONLY NAMES USING UPPERCASE P,S,I,J,K AND LOWERCASE P,S,I,C ALLOWED .
C LOWERCASE P AND S MUST BE FIRST CHARACTER .
C IF THE MANTLE , CORE OR INNER CORE INCLUDE INTERFACES , THEN THE
C SUBROUTINE GENERATES THE RAY TURNING IN THE DEEPEST LAYER . REMEMBER
C THE REFLECTION AND TURNING RAY , E.G. P AND PCP , HAVE SIMILAR INTEGER
C DEFINITIONS AND ARE DISTINGUISHED ELSEWHERE BY THE RAY PARAMETER RANGE
      CHARACTER*16 IA
      INTEGER NP(ILAY),NS(ILAY),ID(10,ILAY)
C SET ARRAYS TO ZERO
      DO 9 I=1,ILAY
      NP(I)=0
      NS(I)=0
      DO 9 J=1,10
9     ID(J,I)=0
C DECODE RAY NAME
C COMMENT CARDS REFER TO UPPERCASE LETTERS UNLESS SPECIFICALLY STATED
      J1=0
      DO 10 I=1,16
      J=JCHAR(IA(I:I))-9
      GO TO (10,11,12,13,14,15,16,17,18,19,191,191,191),J
      GO TO 20
C**** P ****
11    GO TO (20,111,112,20,20,115,116,117,20,119,1111,1112,1113),J1
C J1=0 SO THIS IS P SOURCE
      ISS=1
      CALL ADDRAY (NP,ID(2,1),ICS,ICM-1,1)
      GO TO 100
C P FOLLOWS P
111   ID(3,1)=ID(3,1)+1
      IF (J2.EQ.6.OR.J2.EQ.10) GO TO 110
      ID(1,ICM)=ID(1,ICM)+1
      CALL ADDRAY (NP,ID(2,1),1,ICM-1,2)
      GO TO 100
c --- P follows d
1111  ID(3,1)=ID(3,1)+1
      if (j2.ne.2) goto 20
      ID(1,ICM)=ID(1,ICM)+1
      CALL ADDRAY (NP,ID(2,1),i660,ICM-1,2)
      GO TO 100
c --- P follows e
1112  ID(3,1)=ID(3,1)+1
      if (j2.ne.2) goto 20
      ID(1,ICM)=ID(1,ICM)+1
      CALL ADDRAY (NP,ID(2,1),i400,ICM-1,2)
      GO TO 100
c --- P follows f
1113  ID(3,1)=ID(3,1)+1
      if (j2.ne.2) goto 20
      ID(1,ICM)=ID(1,ICM)+1
      CALL ADDRAY (NP,ID(2,1),i220,ICM-1,2)
      GO TO 100
C P FOLLOWS S
112   ID(10,1)=ID(10,1)+1
      IF (J2.EQ.6.OR.J2.EQ.10) GO TO 110
      ID(4,ICM)=ID(4,ICM)+1
      CALL ADDRAY (NS,ID(5,1),1,ICM-1,1)
      GO TO 110
C P FOLLOWS K
115   ID(2,ICM)=ID(2,ICM)+1
      IF (J2.EQ.4.OR.J2.EQ.5.OR.J2.EQ.9) GO TO 110
      ID(1,ICI)=ID(1,ICI)+1
      CALL ADDRAY (NP,ID(2,1),ICM,ICI-1,1)
      GO TO 110
C P FOLLOWS LOWERCASE P
116   ID(3,1)=ID(3,1)+1
      GO TO 110
C P FOLLOWS LOWERCASE S
117   ID(10,1)=ID(10,1)+1
      GO TO 110
C P FOLLOWS LOWERCASE C
119   IF (J2.EQ.2) ID(1,ICM)=ID(1,ICM)+1
      IF (J2.EQ.3) ID(7,ICM)=ID(7,ICM)+1
C P MANTLE SEGMENT
110   CALL ADDRAY (NP,ID(2,1),1,ICM-1,1)
      GO TO 100
C**** S ****
12    GO TO (20,121,122,20,20,125,126,127,20,129,1221,1222,1223),J1
C J1=0 SO THIS IS S SOURCE RAY
      ISS=2
      CALL ADDRAY (NS,ID(5,1),ICS,ICM-1,1)
      GO TO 100
C S FOLLOWS P
121   ID(10,1)=ID(10,1)+1
      IF (J2.EQ.6.OR.J2.EQ.10) GO TO 120
      ID(1,ICM)=ID(1,ICM)+1
      CALL ADDRAY (NP,ID(2,1),1,ICM-1,1)
      GO TO 120
C S FOLLOWS S
122   ID(6,1)=ID(6,1)+1
      IF (J2.EQ.6.OR.J2.EQ.10) GO TO 120
      ID(4,ICM)=ID(4,ICM)+1
      CALL ADDRAY (NS,ID(5,1),1,ICM-1,2)
      GO TO 100
c --- S follows d 
1221  ID(6,1)=ID(6,1)+1
      if (j2.ne.3) goto 20
      ID(4,ICM)=ID(4,ICM)+1
      CALL ADDRAY (NS,ID(5,1),i670,ICM-1,2)
      GO TO 100
c --- S follows e
1222  ID(6,1)=ID(6,1)+1
      if (j2.ne.3) goto 20
      ID(4,ICM)=ID(4,ICM)+1
      CALL ADDRAY (NS,ID(5,1),i400,ICM-1,2)
      GO TO 100
c --- S follows f 
1223  ID(6,1)=ID(6,1)+1
      if (j2.ne.3) goto 20
      ID(4,ICM)=ID(4,ICM)+1
      CALL ADDRAY (NS,ID(5,1),i220,ICM-1,2)
      GO TO 100
C S FOLLOWS K
125   ID(9,ICM)=ID(9,ICM)+1
      IF (J2.EQ.4.OR.J2.EQ.5.OR.J2.EQ.9) GO TO 120
      ID(1,ICI)=ID(1,ICI)+1
      CALL ADDRAY (NP,ID(2,1),ICM,ICI-1,1)
      GO TO 120
C S FOLLOWS LOWERCASE P
126   ID(10,1)=ID(10,1)+1
      GO TO 120
C S FOLLOWS LOWERCASE S
127   ID(6,1)=ID(6,1)+1
      GO TO 120
C S FOLLOWS LOWERCASE C
129   IF (J2.EQ.2) ID(7,ICM)=ID(7,ICM)+1
      IF (J2.EQ.3) ID(4,ICM)=ID(4,ICM)+1
C S MANTLE SEGMENT
120   CALL ADDRAY (NS,ID(5,1),1,ICM-1,1)
      GO TO 100
C**** I ****
13    GO TO (20,20,20,133,134,135,20,20,20,20),J1
      GO TO 20
C I FOLLOWS I
133   ID(3,ICI)=ID(3,ICI)+1
      GO TO 130
C I FOLLOWS J
134   ID(10,ICI)=ID(10,ICI)+1
      GO TO 130
C I FOLLOWS K
135   ID(2,ICI)=ID(2,ICI)+1
C I IN INNER CORE
C---------- ILAY IS TOTAL NUMBER OF LAYERS. LAST LAYER IS INNER CORE.
C130   CALL ADDRAY (NP,ID(2,1),ICI,ILAY-1,2)
130   CALL ADDRAY (NP,ID(2,1),ICI,ILAY,2)
C     ID(1,ILAY)=ID(1,ILAY)+1
      GO TO 100
C**** J ****
14    GO TO (20,20,20,143,144,145,20,20,20,20),J1
      GO TO 20
C J FOLLOWS I
143   ID(10,ICI)=ID(10,ICI)+1
      GO TO 140
C J FOLLOWS J
144   ID(6,ICI)=ID(6,ICI)+1
      GO TO 140
C J FOLLOWS K
145   ID(8,ICI)=ID(8,ICI)+1
C J IN INNER CORE
C140   CALL ADDRAY (NS,ID(5,1),ICI,ILAY-1,2)
140   CALL ADDRAY (NS,ID(5,1),ICI,ILAY,2)
C     ID(4,ILAY)=ID(4,ILAY)+1
      GO TO 100
C**** K ****
15    GO TO (20,151,152,153,154,155,20,20,158,20),J1
      GO TO 20
C K FOLLOWS P
151   ID(2,ICM)=ID(2,ICM)+1
      GO TO 150
C K FOLLOWS S
152   ID(9,ICM)=ID(9,ICM)+1
      GO TO 150
C K FOLLOWS I
153   ID(2,ICI)=ID(2,ICI)+1
      GO TO 150
C K FOLLOWS J
154   ID(8,ICI)=ID(8,ICI)+1
      GO TO 150
C K FOLLOWS K
155   ID(3,ICM)=ID(3,ICM)+1
      IF (J2.EQ.4.OR.J2.EQ.5.OR.J2.EQ.9) GO TO 150
      ID(1,ICI)=ID(1,ICI)+1
      CALL ADDRAY (NP,ID(2,1),ICM,ICI-1,2)
      GO TO 100
C K FOLLOWS LOWERCASE I
158   ID(1,ICI)=ID(1,ICI)+1
C K IN CORE
150   CALL ADDRAY (NP,ID(2,1),ICM,ICI-1,1)
      GO TO 100
C**** LOWERCASE P ****
16    IF (J1.NE.0) GO TO 20
      ISS=5
      CALL ADDRAY (NP,ID(2,1),1,ICS,1)
      GO TO 100
C**** LOWERCASE S ****
17    IF (J1.NE.0) GO TO 20
      ISS=6
      CALL ADDRAY (NS,ID(5,1),1,ICS,1)
      GO TO 100
C**** LOWERCASE I ****
18    IF (J1.EQ.6) GO TO 100
      GO TO 20
C**** LOWERCASE C ****
19    IF (J1.EQ.2.OR.J1.EQ.3) GO TO 100
      GO TO 20
c --- lowercase d, e, or f 
191	if(j1.eq.2.or.j1.eq.3) goto 100
	goto 20
C
100   J2=J1
      J1=J
10    CONTINUE
C RECEIVER SEGMENT
      GO TO (20,201,202,20,20,20,201,202,20,20),J1
C P RECEIVER
201   IF (J2.EQ.6.OR.J2.EQ.10) GO TO 203
CCCCC--- modified to deal with upgoing direct p
      IF (J1.EQ.7) GO TO 203
CCCCC------------------------------------------
      CALL ADDRAY (NP,ID(2,1),1,ICM-1,1)
      ID(1,ICM)=ID(1,ICM)+1
203   CONTINUE
      IRR=9
      GO TO 200
C S RECEIVER
202   IF (J2.EQ.6.OR.J2.EQ.10) GO TO 204
CCCCC--- modified to deal with upgoing direct s
      IF (J1.EQ.8) GO TO 203
CCCCC------------------------------------------
      CALL ADDRAY (NS,ID(5,1),1,ICM-1,1)
      ID(4,ICM)=ID(4,ICM)+1
204   CONTINUE
      IRR=10
200   J1=ILAY+1
      DO 300 J=1,ILAY
      I=J1-J
      IF (NP(I)+NS(I).NE.0) GO TO 301
300   CONTINUE
301   NN=I+1
C ---------- RAY HAS TO BOTTOM IN LOWEST LAYER
      IF(NN.GT.ILAY) NN=ILAY
      RETURN
C ERROR IN RAY NAME
20    WRITE (6,600) IA
      STOP
600   FORMAT (' ERROR IN RAY NAME ',A16)
      END

c ===

      SUBROUTINE ADDRAY (NPS,ID,IC1,IC2,I)
      INTEGER NPS(IC2),ID(10,IC2)
C ADD I TRANSMITTED RAY SEGMENTS THROUGH LAYERS IC1 TO IC2 .
C WITHOUT COEFFICIENTS AT END POINTS .
      DO 1 J=IC1,IC2
1     NPS(J)=NPS(J)+I
      IF (IC1.EQ.IC2) RETURN
      IC=IC1+1
      DO 2 J=IC,IC2
2     ID(1,J)=ID(1,J)+I
      RETURN
      END

c ===

      INTEGER FUNCTION JCHAR (IA)
      CHARACTER*1 IA,IB(23)
C INTEGER FUNCTION TO DECODE CHARACTERS IN RAY DEFINITION .
C CHARACTERS ARE ( IN ORDER ):
C NUMERALS 0 TO 9 , BLANK , UPPERCASE P,S,I,J,K, AND LOWERCASE P,S,I,C
      DATA IB/'0','1','2','3','4','5','6','7','8','9',' ',
     .'P','S','I','J','K','p','s','i','c','d','e','f'/
      DO 1 I=1,23
      IF (IA.EQ.IB(I)) GO TO 2
1     CONTINUE
      I=24
2     JCHAR=I-1
      RETURN
      END
